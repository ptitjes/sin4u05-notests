plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.7'
}

version = '0.0.0'
sourceCompatibility = 11
targetCompatibility = 11

sourceSets {
    main {
        java {
            srcDirs = [
                    'app/backend/src/main/java',
                    'app/core/src/main/java',
                    'app/frontend/src/main/java',
            ]
        }
        resources {
            srcDirs = [
                    "app/frontend/src/main/resources",
            ]
        }
    }

    test {
        java {
            srcDirs = [
                    'app/backend/src/test/java',
                    'app/core/src/test/java',
                    'app/frontend/src/test/java',
            ]
        }
    }
}

test {
    useJUnitPlatform()
    minHeapSize = "128m"
    maxHeapSize = "1024m"

    // Headless test execution (true by default)
    if (project.hasProperty("headless") ? project.headless : true) {
        jvmArgs "-Dtestfx.robot=glass"
        jvmArgs "-Dtestfx.headless=true"
        jvmArgs "-Dprism.order=sw"
        jvmArgs "-Dprism.text=t2k"
    }
}

javafx {
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.web']
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "com.github.penemue:keap:0.3.0"

    compile 'org.kordamp.ikonli:ikonli-javafx:11.1.0'
    compile 'org.kordamp.ikonli:ikonli-fontawesome5-pack:11.1.0'
    compile 'org.kordamp.ikonli:ikonli-material-pack:11.1.0'
    compile 'org.codehaus.griffon:griffon-javafx:2.15.1'
    compile 'org.controlsfx:controlsfx:11.0.0-RC2'
    compile 'com.lynden:GMapsFX:2.12.0'

    testCompile "org.junit.jupiter:junit-jupiter-api:5.4.0"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.4.0"

    testCompile "org.mockito:mockito-core:2.25.0"
    testCompile "org.mockito:mockito-junit-jupiter:2.25.0"
    testCompile "org.testfx:testfx-junit5:4.0.15-alpha"
    testRuntime "org.testfx:openjfx-monocle:jdk-11+26"
}

application {
    mainClassName = 'fr.univ.amu.sin4u05.igl.transit.ui.Main'
    applicationDefaultJvmArgs = [
            '--add-opens=javafx.base/com.sun.javafx.event=ALL-UNNAMED'
    ]
}

javadoc {
    options.with {
        links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
    }
}
